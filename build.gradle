apply plugin: 'com.android.application'

android {
    namespace 'com.dalton.braillekeyboard'
    compileSdkVersion 33
    buildToolsVersion "28.0.3"

    signingConfigs {
        // Use same key for debug and release so you can update debugâ†”release without reinstall.
        release {
            def kf = System.getenv("KEYSTORE_FILE")
            def kp = System.getenv("KEYSTORE_PASSWORD")
            def ka = System.getenv("KEY_ALIAS")
            def kpw = System.getenv("KEY_PASSWORD")
            if (kf == null || kp == null || ka == null || kpw == null) {
                def local = project.file('local.properties')
                if (local.exists()) {
                    def props = new Properties()
                    props.load(new FileInputStream(local))
                    kf = kf ?: props.getProperty('KEYSTORE_FILE')
                    kp = kp ?: props.getProperty('KEYSTORE_PASSWORD')
                    ka = ka ?: props.getProperty('KEY_ALIAS')
                    kpw = kpw ?: props.getProperty('KEY_PASSWORD')
                }
            }
            if (kf != null && kp != null && ka != null && kpw != null) {
                storeFile file(kf)
                storePassword kp
                keyAlias ka
                keyPassword kpw
            }
        }
    }

    defaultConfig {
        applicationId "com.dalton.braillekeyboard"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 40
        versionName "4.0"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        debug {
            minifyEnabled false
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
        }
        release {
            minifyEnabled false
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    implementation project(':client')
    implementation project(':service')
    implementation 'com.android.support:support-v4:28.0.0'
}
